apply plugin: 'maven-publish'
apply plugin: 'signing'

def isReleaseBuild() {
    return VERSION_NAME.contains("SNAPSHOT") == false
}

def getReleaseRepositoryUrl() {
    return hasProperty('RELEASE_REPOSITORY_URL') ? RELEASE_REPOSITORY_URL
            : "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
}

def getSnapshotRepositoryUrl() {
    return hasProperty('SNAPSHOT_REPOSITORY_URL') ? SNAPSHOT_REPOSITORY_URL
            : "https://s01.oss.sonatype.org/content/repositories/snapshots/"
}

def getRepositoryUsername() {
    return hasProperty('ossrhUsername') ? ossrhUsername : ""
}

def getRepositoryPassword() {
    return hasProperty('ossrhPassword') ? ossrhPassword : ""
}

def getSigningKeyID(){
    return hasProperty("mavenGpgKeyId") ? mavenGpgKeyId : ""
}

def getSigningKey(){
    return hasProperty("mavenGpgKey") ? mavenGpgKey: ""
}

configurations {
    javadocApi
}

afterEvaluate { project ->

    dependencies {
        javadocApi("androidx.annotation:annotation:1.3.0")
    }

    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.sourceFiles
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from "/dev/null"
    }

    publishing {
        repositories {
            maven {
                url = getReleaseRepositoryUrl()
                credentials {
                    username = getRepositoryUsername()
                    password = getRepositoryPassword()
                }
            }
        }
        publications {
            release (MavenPublication) {
                from components.release
                artifact tasks.androidSourcesJar
                artifact tasks.javadocJar
                groupId = 'me.grishka.litex'
                artifactId = project.name
                version = project.version
                pom {
                    name = "LiteX ${project.name.capitalize()}"
                    description = "${project.name} ${project.version}"
                    url = "https://github.com/grishka/LiteX"
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'grishka'
                            name = 'Gregory Klyushnikov'
                            email = 'mavencentral@grishka.me'
                        }
                    }
                    scm {
                        url = 'https://github.com/grishka/LiteX'
                        connection = 'scm:git:git://github.com/grishka/litex.git'
                        developerConnection = 'scm:git:ssh://github.com:grishka/litex.git'
                    }
                }
            }
        }
    }

    signing {
        sign publishing.publications.release
    }
}